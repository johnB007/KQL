//Detects Suspicious PowerShell web activity with cmd.exe as initiating ProcessCommandLine
//Date Created: 04/08/2022 Updated: 04/11/2022 
//Author: Adam Borella
DeviceProcessEvents
| where FileName in~ ("powershell.exe", "powershell_ise.exe") and InitiatingProcessFileName has "cmd.exe" and isnotempty(AccountName) 
| where ProcessCommandLine has_any("webClient", "DownloadFile", "DownloadData", "DownloadString", "WebRequest", "Shellcode", "http", "https")
// excludes certain known command line activity
| where ProcessCommandLine !has "http://169.254.169.254" //AWS related IP use for Cloud VM Configuration
| where ProcessCommandLine !has "nessus" //Scanning tool
| where ProcessCommandLine !has "https://cat.va.dcap.fbi.gov"
| where ProcessCommandLine !has "https://chocolatey.org" //Chocolatey is software management automation for Windows that wraps installers, executables, zips, and scripts into compiled packages.
//excludes certain scanning accounts and admin account that generates significant noise
| where AccountName != "svc_evrast"
//Show specific fields
| project Timestamp, ReportId, DeviceId, DeviceName, AccountName, ActionType, FileName, SHA256, ProcessVersionInfoOriginalFileName, ProcessCommandLine,
 InitiatingProcessSHA256, InitiatingProcessFileName, InitiatingProcessCommandLine