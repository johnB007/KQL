// Lists all the files downloaded using popular browsers.
DeviceFileEvents 
| where Timestamp > ago(7d)
| where FolderPath !has "$Recycle.Bin"
| where 
    // Edge
    InitiatingProcessFolderPath endswith @"windows\system32\browser_broker.exe" 
    // Internet Explorer x64
    or InitiatingProcessFolderPath endswith @"program files\internet explorer\iexplore.exe"
    // Internet Explorer x32
    or InitiatingProcessFolderPath endswith @"program files (x86)\internet explorer\iexplore.exe"
    // Chrome
    or (InitiatingProcessFileName =~ "chrome.exe" and FileName endswith "crdownload")
    // Firefox
    or (InitiatingProcessFileName =~ "firefox.exe" and (FileName !endswith ".js" or FolderPath !has "profile"))
| project Timestamp, DeviceName, InitiatingProcessFileName, FileName, FolderPath
| top 100 by Timestamp 
